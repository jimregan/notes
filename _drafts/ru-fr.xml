<?xml version="1.0" encoding="UTF-8" ?>
<!DOCTYPE supplementalData SYSTEM "../../common/dtd/ldmlSupplemental.dtd">
<supplementalData>
  <transforms>
    <transform source="ru" target="ru_Latn" variant="FR" direction="forward">
      <tRule><![CDATA[
::NFC;
$wordBoundary = [^[:L:][:M:][:N:]];
$upperVowels  = [АЕЁЭИОУЫЮЯ];
$lowerVowels  = [аеёэиоуыюя];
$vowels       = [$upperVowels $lowerVowels];
$upper        = [:Uppercase:];
$lower        = [:Lowercase:];
$CHSH         = [ЧчШшЩщЖж];
$IJ           = [ИиЙй];
$latinVowels  = [AEIOUYÄËÏÖÜŸaeiouyäëïöüÿ];
$latIJ        = [Ïï];
$latI         = [Ii];
$allVowels    = [$vowels $latinVowels];

Г } [ЕЁИЫЭ] → GU;
Г } [еёиыэ] → Gu;
г } [ЕЁИЫЭ] → gu;
г } [еёиыэ] → gu;

$wordBoundary { Е } $lower    → Ie;
$wordBoundary { Е } [^$lower] → IE;
$wordBoundary { е }           → ie;

[ЬьЪъ] { Е } $lower    → ie;
[ЬьЪъ] { Е } [^$lower] → IE;
[ьъ]  { е }            → ie;

[ $IJ $latI $latIJ ] { Е } → E;
[ $IJ $latI $latIJ ] { е } → e;

GU { Е } → E;
GU { е } → E;
Gu { Е } → E;
Gu { е } → e;
gu { Е } → e;
gu { е } → e;

[ ($allVowels-$IJ) ] { Е } $lower    → ïe;
[ ($allVowels-$IJ) ] { Е } [^$lower] → ÏE;
[ ($allVowels-$IJ) ] { е }           → ïe;

$CHSH { Ё } $lower    → e;
$CHSH { Ё } [^$lower] → E;
$CHSH { ё }           → e;
Ё } $lower    → io;
Ё } [^$lower] → IO;
ё             → io;

[$vowels-[Ии]] { И } → Ï;
[$vowels-[Ии]] { и } → ï;

[ИиЬь] { Й } [Ее] → ;
ИЙ $ → I;
Ий $ → i;
ий $ → i;
ЫЙ $ → Y;
Ый $ → y;
ый $ → y;
Й → Ï;
й → ï;

$IJ { Ю } $lower    → ou;
$IJ { Ю } [^$lower] → OU;
$IJ { ю }           → ou;
[($allVowels-$IJ)] { Ю } $lower    → you;
[($allVowels-$IJ)] { Ю } [^$lower] → YOU;
[($allVowels-$IJ)] { ю }           → you;
Ю } $lower    → Iou;
Ю } [^$lower] → IOU;
ю             → iou;

[ $IJ $latI $latIJ ] { Я } $lower    → a;
[ $IJ $latI $latIJ ] { Я } [^$lower] → A;
[ $IJ $latI $latIJ ] { я }           → a;
[($allVowels-$IJ)] { Я } $lower    → ïa;
[($allVowels-$IJ)] { Я } [^$lower] → ÏA;
[($allVowels-$IJ)] { я }           → ïa;
$wordBoundary { Я } $lower    → Ia;
$wordBoundary { Я } [^$lower] → IA;
$wordBoundary { я }           → ia;
Я } $lower    → ia;
Я } [^$lower] → IA;
я             → ia;

$allVowels { С } [$lowerVowels] → Ss;
$allVowels { С }  $upperVowels  → SS;
$allVowels { с }  $allVowels    → ss;

[Ии]ЕВ $ → iev;
ИЕВ $   → IEV;
ЕВ $    → iev;

[ИЫ] { Н } $             → INE;
[иы] { н } $             → ine;
[ИЫ] { Н } $wordBoundary → INE;
[иы] { н } $wordBoundary → ine;

А → A;  а → a;
Б → B;  б → b;
В → V;  в → v;
Г → G;  г → g;
Д → D;  д → d;
Е → E;  е → e;
Ж → J;  ж → j;
З → Z;  з → z;
И → I;  и → i;
К → K;  к → k;
Л → L;  л → l;
М → M;  м → m;
Н → N;  н → n;
О → O;  о → o;
П → P;  п → p;
Р → R;  р → r;
С → S;  с → s;
Т → T;  т → t;
Ф → F;  ф → f;

У } $lower    → Ou;
У } [^$lower] → OU;
у             → ou;
Х } $lower    → Kh;
Х } [^$lower] → KH;
х             → kh;
Ц } $lower    → Ts;
Ц } [^$lower] → TS;
ц             → ts;
Ч } $lower    → Tch;
Ч } [^$lower] → TCH;
ч             → tch;
Ш } $lower    → Ch;
Ш } [^$lower] → CH;
ш             → ch;
Щ } $lower    → Chtch;
Щ } [^$lower] → CHTCH;
щ             → chtch;

Ъ → ;
ъ → ;
Ы → y;  ы → y;
Ь → ;
ь → ;
Э } $lower → e;
Э } [^$lower] → E;
э → e;
::NFC;
      ]]></tRule>
    </transform>
  </transforms>
</supplementalData>
